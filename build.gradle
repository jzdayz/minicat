plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
    id 'java'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.3.71" apply false
}

allprojects {
    group = 'io.github.jzdayz'
    version = '0.0.1.SNAPSHOT'
    sourceCompatibility = '1.8'

    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
        jcenter()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

	def skip = ["naming"]
    publishing {
        if (!skip.contains(project.name)) {

            //发布内容
            publications {
                //自命名"myPublish"为一个发布任务
                maven(MavenPublication) {
                    //jar来源
                    from components.java
                }
            }
            //配置仓库信息
            repositories {
                mavenLocal()
            }

        }

    }

}

subprojects {

    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }


    dependencyManagement {
        imports {
            mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        }
    }

    dependencies {
        // 这里spring boot 的父依赖失效了，后续采用依赖pom不使用plugin
        compileOnly 'org.projectlombok:lombok'
        implementation 'io.netty:netty-all'
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        annotationProcessor 'org.projectlombok:lombok'
    }


    test {
        useJUnitPlatform()
    }
}

